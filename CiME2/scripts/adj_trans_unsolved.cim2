(* 

    Language and Term Rewriting System for "Typed Dirac Notation"

    This language can be used in two levels: deal with the internal untyped language directly,
    or use the typed syntax with type polymorphism.

    Yingte Xu, 2024

*)

let F = signature
"

  (* complex number *)
  + : AC ;
  * : AC ;
  0 : constant ;
  1 : constant ;
  ^* : postfix unary ;

  (* -------- types -------- *)
  T : constant ;
  PROD : infix binary ;

  Base : binary ;
  S : unary ;
  K : binary ;
  B : binary ;
  O : 3 ;
  

  (* -------- syntax symbols -------- *)
  (* The external language. Will be be transformed in to the internal langauge during type checking. *)

  (* Basis *)
  PAIR_S : binary ;
  FST_S : unary ;
  SND_S : unary ;

  (* Scalar *)
  C_S : unary ;
  DELTA_S : binary ;
  ADDS_S : infix binary ;
  MLTS_S : infix binary ;
  CONJS_S : unary ;
  DOT_S : infix binary ;

  (* Ket *)
  0K_S : unary ;
  KET_S : unary ;
  ADJK_S : unary ;
  TRANK_S : unary ;
  SCRK_S : infix binary ;
  ADDK_S : infix binary ;
  MLTK_S : infix binary ;
  TSRK_S : infix binary ;

  (* Bra *)
  0B_S : unary ;
  BRA_S : unary ;
  ADJB_S : unary ;
  TRANB_S : unary ;
  SCRB_S : infix binary ;
  ADDB_S : infix binary ;
  MLTB_S : infix binary ;
  TSRB_S : infix binary ;

  (* Operator *)
  0O_S : binary ;
  1O_S : unary ;
  OUTER_S : infix binary ;
  ADJO_S : unary ;
  TRANO_S : unary ;
  SCRO_S : infix binary ;
  ADDO_S : infix binary ;
  MLTO_S : infix binary ;
  TSRO_S : infix binary ;

  (* universal application *)
  @ : infix binary ;

  (* otimes and cdot *)
  OTIMES : infix binary ;
  CDOT : infix binary ;


  (* -------- internal langauge -------- *)

  (* Basis *)
  PAIR : binary ;
  FST : unary ;
  SND : unary ;

  (* Scalar *)
  C : unary ;
  DELTA : commutative ;
  ADDS : AC ;
  MLTS : AC ;
  CONJS : unary ;
  DOT : infix binary ;

  (* Ket *)
  0K : constant ;
  KET : unary ;
  ADJK : unary ;
  TRANK : unary ;
  SCRK : infix binary ;
  ADDK : AC ;
  MLTK : infix binary ;
  TSRK : infix binary ;

  (* Bra *)
  0B : constant ;
  BRA : unary ;
  ADJB : unary ;
  TRANB : unary ;
  SCRB : infix binary ;
  ADDB : AC ;
  MLTB : infix binary ;
  TSRB : infix binary ;

  (* Operator *)
  0O : constant ;
  1O : constant ;
  OUTER : infix binary ;
  ADJO : unary ;
  TRANO : unary ;
  SCRO : infix binary ;
  ADDO : AC ;
  MLTO : infix binary ;
  TSRO : infix binary ;

";

let X = vars "a b c x S0 S1 S2 S3 s s1 s2 t t1 t2 B0 B1 B2 K0 K1 K2 O0 O1 O2 O1' O2' T1 T2 T3 T4";

let R = TRS F X "

  (* ######################## TYPE CHECKING ############################ *)
  C_S(a) -> S(C(a)) ;
  DELTA_S(Base(s, T1), Base(t, T1)) -> S(DELTA(s, t)) ;
  S(a) ADDS_S S(b) -> S(a ADDS b) ;
  S(a) MLTS_S S(b) -> S(a MLTS b) ;
  CONJS_S(S(a)) -> S(CONJS(a)) ;
  B(B0, T1) DOT_S K(K0, T1) -> S(B0 DOT K0) ;

  (* Ket *)
  0K_S(T1) -> K(0K, T1) ;
  KET_S(Base(s, T1)) -> K(KET(s), T1) ;
  ADJK_S(B(B0, T1)) -> K(ADJK(B0), T1) ;
  S(S0) SCRK_S K(K0, T1) -> K(S0 SCRK K0, T1) ;
  K(K1, T1) ADDK_S K(K2, T1) -> K(K1 ADDK K2, T1) ;
  O(O0, T1, T2) MLTK_S K(K0, T2) -> K(O0 MLTK K0, T1) ;
  K(K1, T1) TSRK_S K(K2, T2) -> K(K1 TSRK K2, T1 PROD T2) ;

  (* Bra *)
  0B_S(T1) -> B(0B, T1) ;
  BRA_S(Base(s, T1)) -> B(BRA(s), T1) ;
  ADJB_S(K(K0, T1)) -> B(ADJB(K0), T1) ;
  S(S0) SCRB_S B(B0, T1) -> B(S0 SCRB B0, T1) ;
  B(B1, T1) ADDB_S B(B2, T1) -> B(B1 ADDB B2, T1) ;
  B(B0, T1) MLTB_S O(O0, T1, T2) -> B(B0 MLTB O0, T2) ;
  B(B1, T1) TSRB_S B(B2, T2) -> B(B1 TSRB B2, T1 PROD T2) ;

  (* Operator *)
  0O_S(T1, T2) -> O(0O, T1, T2) ;
  1O_S(T1) -> O(1O, T1, T1) ;
  K(K0, T1) OUTER_S B(B0, T2) -> O(K0 OUTER B0, T1, T2) ;
  ADJO_S(O(O0, T1, T2)) -> O(ADJO(O0), T2, T1) ;
  S(S0) SCRO_S O(O0, T1, T2) -> O(S0 SCRO O0, T1, T2) ;
  O(O1, T1, T2) ADDO_S O(O2, T1, T2) -> O(O1 ADDO O2, T1, T2) ;
  O(O1, T1, T2) MLTO_S O(O2, T2, T3) -> O(O1 MLTO O2, T1, T3) ;
  O(O1, T1, T2) TSRO_S O(O2, T3, T4) -> O(O1 TSRO O2, T1 PROD T3, T2 PROD T4) ;

  (* -------- overload the universal application --------- *)
  S(S1) @ S(S2) -> S(S1 MLTS S2) ;
  S(S0) @ K(K0, T1) -> K(S0 SCRK K0, T1) ;
  S(S0) @ B(B0, T1) -> B(S0 SCRB B0, T1) ;

  S(S0) @ O(O0, T1, T2) -> O(S0 SCRO O0, T1, T2) ;
  K(K0, T1) @ S(S0) -> K(S0 SCRK K0, T1) ;
  K(K1, T1) @ K(K2, T2) -> K(K1 TSRK K2, T1 PROD T2) ;
  K(K0, T1) @ B(B0, T2) -> O(K0 OUTER B0, T1, T2) ;

  B(B0, T1) @ S(S0) -> B(S0 SCRB B0, T1) ;
  B(B0, T1) @ K(K0, T1) -> S(B0 DOT K0) ;
  B(B1, T1) @ B(B2, T2) -> B(B1 TSRB B2, T1 PROD T2) ;
  B(B0, T1) @ O(O0, T1, T2) -> B(B0 MLTB O0, T2) ;

  O(O0, T1, T2) @ S(S0) -> O(S0 SCRO O0, T1, T2) ;
  O(O0, T1, T2) @ K(K0, T2) -> K(O0 MLTK K0, T1) ;
  O(O1, T1, T2) @ O(O2, T2, T3) -> O(O1 MLTO O2, T1, T3) ;

  (* -------- overload of otimes and cdot -------- *)
  K(K1, T1) OTIMES K(K2, T2) -> K(K1 TSRK K2, T1 PROD T2) ;
  B(B1, T1) OTIMES B(B2, T2) -> B(B1 TSRB B2, T1 PROD T2) ;
  K(K0, T1) OTIMES B(B0, T2) -> O(K0 OUTER B0, T1, T2) ;
  O(O1, T1, T2) OTIMES O(O2, T3, T4) -> O(O1 TSRO O2, T1 PROD T3, T2 PROD T4) ;

  B(B0, T1) CDOT O(O0, T1, T2) -> B(B0 MLTB O0, T2) ;
  O(O0, T1, T2) CDOT K(K0, T2) -> K(O0 MLTK K0, T1) ;
  B(B0, T1) CDOT K(K0, T1) -> S(B0 DOT K0) ;
  O(O1, T1, T2) CDOT O(O2, T2, T3) -> O(O1 MLTO O2, T1, T3) ;




  (* ############# REDUCTION RULES FOR INTERNAL LANGUAGE ############### *)

  (********************************************)
  (*           complex number (avatar)        *)
  (********************************************)

  0 + a -> a ;
  0 * a -> 0 ;
  1 * a -> a ;
  a * (b + c) -> (a * b) + (a * c) ;
  0 ^* -> 0 ;
  1 ^* -> 1 ;
  (a + b) ^* -> (a ^*) + (b ^*) ;
  (a * b) ^* -> (a ^*) * (b ^*) ;
  (a ^*) ^* -> a ;

  (********************************************)
  (*                 Basis                    *)
  (********************************************)

  FST(PAIR(s, t)) -> s ;
  SND(PAIR(s, t)) -> t ;
  PAIR(FST(t), SND(t)) -> t ;


  (********************************************)
  (*                 Scalars                  *)
  (********************************************)

  (* ============== Delta Operator ================ *)

  DELTA(s, PAIR(t1, t2)) -> DELTA(FST(s), t1) MLTS DELTA(SND(s), t2) ;
  DELTA(FST(s), FST(t)) MLTS DELTA(SND(s), SND(t)) -> DELTA(s, t) ;

  (* ============================================== *)

  C(0) ADDS a -> a ;
  C(a) ADDS C(b) -> C(a + b) ;
  S0 ADDS S0 -> C(1 + 1) MLTS S0 ;
  (C(a) MLTS S0) ADDS S0 -> C(a + 1) MLTS S0 ;
  (C(a) MLTS S0) ADDS (C(b) MLTS S0) -> C(a + b) MLTS S0 ;

  C(0) MLTS a -> C(0) ;
  C(1) MLTS a -> a ;
  C(a) MLTS C(b) -> C(a * b) ;
  S1 MLTS (S2 ADDS S3) -> (S1 MLTS S2) ADDS (S1 MLTS S3) ;


  CONJS(C(a)) -> C(a ^*) ;
  CONJS(DELTA(s, t)) -> DELTA(s, t) ;
  CONJS(S1 ADDS S2) -> CONJS(S1) ADDS CONJS(S2) ;
  CONJS(S1 MLTS S2) -> CONJS(S1) MLTS CONJS(S2) ;
  CONJS(CONJS(S0)) -> S0 ;
  CONJS(B0 DOT K0) -> ADJB(K0) DOT ADJK(B0) ;

  0B DOT K0 -> C(0) ;
  B0 DOT 0K -> C(0) ;
  (S0 SCRB B0) DOT K0 -> S0 MLTS (B0 DOT K0) ;
  B0 DOT (S0 SCRK K0) -> S0 MLTS (B0 DOT K0) ;
  (B1 ADDB B2) DOT K0 -> (B1 DOT K0) ADDS (B2 DOT K0) ;
  B0 DOT (K1 ADDK K2) -> (B0 DOT K1) ADDS (B0 DOT K2) ;
  BRA(s) DOT KET(t) -> DELTA(s, t) ;
  (B1 TSRB B2) DOT KET(t) -> (B1 DOT KET(FST(t))) MLTS (B2 DOT KET(SND(t))) ;
  BRA(t) DOT (K1 TSRK K2) -> (BRA(FST(t)) DOT K1) MLTS (BRA(SND(t)) DOT K2) ;
  (B1 TSRB B2) DOT (K1 TSRK K2) -> (B1 DOT K1) MLTS (B2 DOT K2) ;

  (B0 MLTB O0) DOT K0 -> B0 DOT (O0 MLTK K0) ;
  BRA(s) DOT ((O1 TSRO O2) MLTK K0) -> ((BRA(FST(s)) MLTB O1) TSRB (BRA(SND(s)) MLTB O2)) DOT K0 ;
  (B1 TSRB B2) DOT ((O1 TSRO O2) MLTK K0) -> ((B1 MLTB O1) TSRB (B2 MLTB O2)) DOT K0 ;


  (********************************************)
  (*            Ket and Bra                   *)
  (********************************************)


  (* >>>> Conjugate Transpose <<<< *)
  ADJK(0B) -> 0K ;
  ADJK(BRA(t)) -> KET(t) ;
  ADJK(ADJB(K0)) -> K0 ;
  ADJK(S0 SCRB B0) -> CONJS(S0) SCRK ADJK(B0) ;
  ADJK(B1 ADDB B2) -> ADJK(B1) ADDK ADJK(B2) ;
  ADJK(B0 MLTB O0) -> ADJO(O0) MLTK ADJK(B0) ;
  ADJK(B1 TSRB B2) -> ADJK(B1) TSRK ADJK(B2) ;

  ADJB(0K) -> 0B ;
  ADJB(KET(t)) -> BRA(t) ;
  ADJB(ADJK(B0)) -> B0 ;
  ADJB(S0 SCRK K0) -> CONJS(S0) SCRB ADJB(K0) ;
  ADJB(K1 ADDK K2) -> ADJB(K1) ADDB ADJB(K2) ;
  ADJB(O0 MLTK K0) -> ADJB(K0) MLTB ADJO(O0) ;
  ADJB(K1 TSRK K2) -> ADJB(K1) TSRB ADJB(K2) ;

  (* >>>> Transpose <<<< *)
  TRANK(0B) -> 0K ;
  TRANK(BRA(t)) -> KET(t) ;
  TRANK(ADJB(K0)) -> ADJK(TRANB(K0)) ;
  TRANK(TRANB(K0)) -> K0 ;
  TRANK(S0 SCRB B0) -> S0 SCRK TRANK(B0) ;
  TRANK(B1 ADDB B2) -> TRANK(B1) ADDK TRANK(B2) ;
  TRANK(B0 MLTB O0) -> TRANO(O0) MLTK TRANK(B0) ;
  TRANK(B1 TSRB B2) -> TRANK(B1) TSRK TRANK(B2) ;

  TRANB(0K) -> 0B ;
  TRANB(KET(t)) -> BRA(t) ;
  TRANB(ADJK(B0)) -> ADJB(TRANK(B0)) ;
  TRANB(TRANK(B0)) -> B0 ;
  TRANB(S0 SCRK K0) -> S0 SCRB TRANB(K0) ;
  TRANB(K1 ADDK K2) -> TRANB(K1) ADDB TRANB(K2) ;
  TRANB(O0 MLTK K0) -> TRANB(K0) MLTB TRANO(O0) ;
  TRANB(K1 TSRK K2) -> TRANB(K1) TSRB TRANB(K2) ;


  (* >>>> Scalar Multiplication <<<< *)
  C(0) SCRK K0 -> 0K ;
  C(1) SCRK K0 -> K0 ;
  S0 SCRK 0K -> 0K ;
  S1 SCRK (S2 SCRK K0) -> (S1 MLTS S2) SCRK K0 ;
  S0 SCRK (K1 ADDK K2) -> (S0 SCRK K1) ADDK (S0 SCRK K2) ;

  C(0) SCRB B0 -> 0B ;
  C(1) SCRB B0 -> B0 ;
  S0 SCRB 0B -> 0B ;
  S1 SCRB (S2 SCRB B0) -> (S1 MLTS S2) SCRB B0 ;
  S0 SCRB (B1 ADDB B2) -> (S0 SCRB B1) ADDB (S0 SCRB B2) ;

  (* >>>> Addition <<<< *)
  K0 ADDK 0K -> K0 ;
  K0 ADDK K0 -> C(1 + 1) SCRK K0 ;
  (S0 SCRK K0) ADDK K0 -> (S0 ADDS C(1)) SCRK K0 ;
  (S1 SCRK K0) ADDK (S2 SCRK K0) -> (S1 ADDS S2) SCRK K0 ;

  B0 ADDB 0B -> B0 ;
  B0 ADDB B0 -> C(1 + 1) SCRB B0 ;
  (S0 SCRB B0) ADDB B0 -> (S0 ADDS C(1)) SCRB B0 ;
  (S1 SCRB B0) ADDB (S2 SCRB B0) -> (S1 ADDS S2) SCRB B0 ;


  (* >>>> Multiplication <<<< *)
  0O MLTK K0 -> 0K ;
  O0 MLTK 0K -> 0K ;
  1O MLTK K0 -> K0 ;
  (S0 SCRO O0) MLTK K0 -> S0 SCRK (O0 MLTK K0) ;
  O0 MLTK (S0 SCRK K0) -> S0 SCRK (O0 MLTK K0) ;
  (O1 ADDO O2) MLTK K0 -> (O1 MLTK K0) ADDK (O2 MLTK K0) ;
  O0 MLTK (K1 ADDK K2) -> (O0 MLTK K1) ADDK (O0 MLTK K2) ;
  (K1 OUTER B0) MLTK K2 -> (B0 DOT K2) SCRK K1 ;
  (O1 MLTO O2) MLTK K0 -> O1 MLTK (O2 MLTK K0) ;
  (O1 TSRO O2) MLTK ((O1' TSRO O2') MLTK K0) -> ((O1 MLTO O1') TSRO (O2 MLTO O2')) MLTK K0 ;
  (O1 TSRO O2) MLTK KET(t) -> (O1 MLTK KET(FST(t))) TSRK (O2 MLTK KET(SND(t))) ;
  (O1 TSRO O2) MLTK (K1 TSRK K2) -> (O1 MLTK K1) TSRK (O2 MLTK K2) ;

  B0 MLTB 0O -> 0B ;
  0B MLTB O0 -> 0B ;
  B0 MLTB 1O -> B0 ;
  B0 MLTB (S0 SCRO O0) -> S0 SCRB (B0 MLTB O0) ;
  (S0 SCRB B0) MLTB O0 -> S0 SCRB (B0 MLTB O0) ;
  B0 MLTB (O1 ADDO O2) -> (B0 MLTB O1) ADDB (B0 MLTB O2) ;
  (B1 ADDB B2) MLTB O0 -> (B1 MLTB O0) ADDB (B2 MLTB O0) ;
  B1 MLTB (K0 OUTER B2) -> (B1 DOT K0) SCRB B2 ;
  B0 MLTB (O1 MLTO O2) -> (B0 MLTB O1) MLTB O2 ;
  (B0 MLTB (O1' TSRO O2')) MLTB (O1 TSRO O2) -> B0 MLTB ((O1' MLTO O1) TSRO (O2' MLTO O2)) ;
  BRA(t) MLTB (O1 TSRO O2) -> (BRA(FST(t)) MLTB O1) TSRB (BRA(SND(t)) MLTB O2) ;
  (B1 TSRB B2) MLTB (O1 TSRO O2) -> (B1 MLTB O1) TSRB (B2 MLTB O2) ;

  (* >>>> Tensor Product <<<< *)
  0K TSRK K0 -> 0K ;
  K0 TSRK 0K -> 0K ;
  KET(s) TSRK KET(t) -> KET(PAIR(s, t)) ;
  (S0 SCRK K1) TSRK K2 -> S0 SCRK (K1 TSRK K2) ;
  K1 TSRK (S0 SCRK K2) -> S0 SCRK (K1 TSRK K2) ;
  (K1 ADDK K2) TSRK K0 -> (K1 TSRK K0) ADDK (K2 TSRK K0) ;
  K0 TSRK (K1 ADDK K2) -> (K0 TSRK K1) ADDK (K0 TSRK K2) ;

  0B TSRB B0 -> 0B ;
  B0 TSRB 0B -> 0B ;
  BRA(s) TSRB BRA(t) -> BRA(PAIR(s, t)) ;
  (S0 SCRB B1) TSRB B2 -> S0 SCRB (B1 TSRB B2) ;
  B1 TSRB (S0 SCRB B2) -> S0 SCRB (B1 TSRB B2) ;
  (B1 ADDB B2) TSRB B0 -> (B1 TSRB B0) ADDB (B2 TSRB B0) ;
  B0 TSRB (B1 ADDB B2) -> (B0 TSRB B1) ADDB (B0 TSRB B2) ;

  (********************************************)
  (*             Operators                    *)
  (********************************************)

  (* >>>> Outer Product <<<< *)
  0K OUTER B0 -> 0O ;
  K0 OUTER 0B -> 0O ;
  (S0 SCRK K0) OUTER B0 -> S0 SCRO (K0 OUTER B0) ;
  K0 OUTER (S0 SCRB B0) -> S0 SCRO (K0 OUTER B0) ;
  (K1 ADDK K2) OUTER B0 -> (K1 OUTER B0) ADDO (K2 OUTER B0) ;
  K0 OUTER (B1 ADDB B2) -> (K0 OUTER B1) ADDO (K0 OUTER B2) ;

  (* >>>> Transpose <<<< *)
  TRANO(0O) -> 0O ;
  TRANO(1O) -> 1O ;
  TRANO(K0 OUTER B0) -> TRANK(B0) OUTER TRANB(K0) ;
  TRANO(ADJO(O0)) -> ADJO(TRANO(O0)) ;
  TRANO(TRANO(O0)) -> O0 ;
  TRANO(S0 SCRO O0) -> S0 SCRO TRANO(O0) ;
  TRANO(O1 ADDO O2) -> TRANO(O1) ADDO TRANO(O2) ;
  TRANO(O1 MLTO O2) -> TRANO(O2) MLTO TRANO(O1) ;
  TRANO(O1 TSRO O2) -> TRANO(O1) TSRO TRANO(O2) ;

  (* >>>> Conjugate Transpose <<<< *)
  ADJO(0O) -> 0O ;
  ADJO(1O) -> 1O ;
  ADJO(K0 OUTER B0) -> ADJK(B0) OUTER ADJB(K0) ;
  ADJO(ADJO(O0)) -> O0 ;
  ADJO(S0 SCRO O0) -> CONJS(S0) SCRO ADJO(O0) ;
  ADJO(O1 ADDO O2) -> ADJO(O1) ADDO ADJO(O2) ;
  ADJO(O1 MLTO O2) -> ADJO(O2) MLTO ADJO(O1) ;
  ADJO(O1 TSRO O2) -> ADJO(O1) TSRO ADJO(O2) ;

  (* >>>> Scalar Multiplication <<<< *)
  C(0) SCRO O0 -> 0O ;
  C(1) SCRO O0 -> O0 ;
  S0 SCRO 0O -> 0O ;
  S1 SCRO (S2 SCRO O0) -> (S1 MLTS S2) SCRO O0 ;
  S0 SCRO (O1 ADDO O2) -> (S0 SCRO O1) ADDO (S0 SCRO O2) ;

  (* >>>> Addition <<<< *)
  O0 ADDO 0O -> O0 ;
  O0 ADDO O0 -> C(1 + 1) SCRO O0 ;
  (S0 SCRO O0) ADDO O0 -> (S0 ADDS C(1)) SCRO O0 ;
  (S1 SCRO O0) ADDO (S2 SCRO O0) -> (S1 ADDS S2) SCRO O0 ;


  (* >>>> Multiplication <<<< *)
  0O MLTO O0 -> 0O ;
  O0 MLTO 0O -> 0O ;
  1O MLTO O0 -> O0 ;
  O0 MLTO 1O -> O0 ;
  (K0 OUTER B0) MLTO O0 -> K0 OUTER (B0 MLTB O0) ;
  O0 MLTO (K0 OUTER B0) -> (O0 MLTK K0) OUTER B0 ;
  (S0 SCRO O1) MLTO O2 -> S0 SCRO (O1 MLTO O2) ;
  O1 MLTO (S0 SCRO O2) -> S0 SCRO (O1 MLTO O2) ;
  (O1 ADDO O2) MLTO O0 -> (O1 MLTO O0) ADDO (O2 MLTO O0) ;
  O0 MLTO (O1 ADDO O2) -> (O0 MLTO O1) ADDO (O0 MLTO O2) ;
  (O1 MLTO O2) MLTO O0 -> O1 MLTO (O2 MLTO O0) ;
  (O1 TSRO O2) MLTO (O1' TSRO O2') -> (O1 MLTO O1') TSRO (O2 MLTO O2') ;
  (O1 TSRO O2) MLTO ((O1' TSRO O2') MLTO O0) -> ((O1 MLTO O1') TSRO (O2 MLTO O2')) MLTO O0 ;

  (* >>>> Tensor Product <<<< *)
  0O TSRO O0 -> 0O ;
  O0 TSRO 0O -> 0O ;
  (K1 OUTER B1) TSRO (K2 OUTER B2) -> (K1 TSRK K2) OUTER (B1 TSRB B2) ;
  (S0 SCRO O1) TSRO O2 -> S0 SCRO (O1 TSRO O2) ;
  O1 TSRO (S0 SCRO O2) -> S0 SCRO (O1 TSRO O2) ;
  (O1 ADDO O2) TSRO O0 -> (O1 TSRO O0) ADDO (O2 TSRO O0) ;
  O0 TSRO (O1 ADDO O2) -> (O0 TSRO O1) ADDO (O0 TSRO O2) ;

  
  (* ################# extension rules for AC symbols ################## *)

  (* To prove the confluence of a system with AC symbols, we need to check the critical pairs of an extended system. *)

  (0 + a) + x -> a + x ;
  (0 * a) * x -> 0 * x ;
  (1 * a) * x -> a * x ;
  (a * (b + c)) * x -> ((a * b) + (a * c)) * x ;

  (C(0) ADDS a) ADDS x -> a ADDS x ;
  (C(a) ADDS C(b)) ADDS x -> C(a + b) ADDS x ;
  (S0 ADDS S0) ADDS x -> (C(1 + 1) MLTS S0) ADDS x ;
  ((C(a) MLTS S0) ADDS S0) ADDS x -> (C(a + 1) MLTS S0) ADDS x ;
  ((C(a) MLTS S0) ADDS (C(b) MLTS S0)) ADDS x -> (C(a + b) MLTS S0) ADDS x ;

  (C(0) MLTS a) MLTS x -> C(0) MLTS x ;
  (C(1) MLTS a) MLTS x -> a MLTS x ;
  (C(a) MLTS C(b)) MLTS x -> C(a * b) MLTS x ;
  (S1 MLTS (S2 ADDS S3)) MLTS x -> ((S1 MLTS S2) ADDS (S1 MLTS S3)) MLTS x ;

  (K0 ADDK 0K) ADDK x -> K0 ADDK x ;
  (K0 ADDK K0) ADDK x -> (C(1 + 1) SCRK K0) ADDK x ;
  ((S0 SCRK K0) ADDK K0) ADDK x -> ((S0 ADDS C(1)) SCRK K0) ADDK x ;
  ((S1 SCRK K0) ADDK (S2 SCRK K0)) ADDK x -> ((S1 ADDS S2) SCRK K0) ADDK x ;

  (B0 ADDB 0B) ADDB x -> B0 ADDB x ;
  (B0 ADDB B0) ADDB x -> (C(1 + 1) SCRB B0) ADDB x ;
  ((S0 SCRB B0) ADDB B0) ADDB x -> ((S0 ADDS C(1)) SCRB B0) ADDB x ;
  ((S1 SCRB B0) ADDB (S2 SCRB B0)) ADDB x -> ((S1 ADDS S2) SCRB B0) ADDB x ;

  (O0 ADDO 0O) ADDO x -> O0 ADDO x ;
  (O0 ADDO O0) ADDO x -> (C(1 + 1) SCRO O0) ADDO x ;
  ((S0 SCRO O0) ADDO O0) ADDO x -> ((S0 ADDS C(1)) SCRO O0) ADDO x ;
  ((S1 SCRO O0) ADDO (S2 SCRO O0)) ADDO x -> ((S1 ADDS S2) SCRO O0) ADDO x ;
  
";

(* optional : 

  DELTA(s, s) -> C(1) ;

*)