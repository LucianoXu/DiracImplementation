obj RATIONAL is 
  sorts Int Rat .
  op 0 : -> Int .
  ops 1z jz 0z abs signe test_signe_pos test_signe_neg : Int -> Int .
  ops f test_abs_pos test_abs_neg min min' min'' : Int Int -> Int .
  ops test_min_pos test_min_neg : Int Int Int -> Int .

  ops + * : Int Int -> Int [assoc comm].
  op neg : Int -> Int .
  op - : Int Int -> Int .

  ops rat irred : Int Int -> Rat .
  ops plus times : Rat Rat -> Rat [comm] .

  vars x y z u v w : Int .

  eq  0z(0) = 0 .

  eq  +(x, 0) = x .
  eq  +(0z(x), 0z(y)) = 0z(+(x, y)) .
  eq  +(0z(x), 1z(y)) = 1z(+(x, y)) .
  eq  +(0z(x), jz(y)) = jz(+(x, y)) .
  eq  +(1z(x), 1z(y)) = jz(+(+(x, y), 1z(0))) .
  eq  +(jz(x), jz(y)) = 1z(+(+(x, y), jz(0))) .
  eq  +(1z(x), jz(y)) = 0z(+(x, y)) .

  eq  neg(0) = 0 .
  eq  neg(0z(x)) = 0z(neg(x)) .
  eq  neg(1z(x)) = jz(neg(x)) .
  eq  neg(jz(x)) = 1z(neg(x)) .

  eq  -(x,y) = +(x ,neg(y)) .

  eq  *(x, 0) = 0 .
  eq  *(x, 0z(y)) = 0z(*(x, y)) .
  eq  *(x, 1z(y)) = +(0z(*(x, y)), x) .
  eq  *(x, jz(y)) = +(0z(*(x, y)), neg(x)) .

  eq  abs(x) = test_abs_pos(x,x) .

  eq  test_abs_pos(0,x) = x .
  eq  test_abs_pos(0z(x),y) = test_abs_pos(x,y) .
  eq  test_abs_pos(1z(x),y) = test_abs_pos(x,y) .
  eq  test_abs_pos(jz(x),y) = test_abs_neg(x,y) .

  eq  test_abs_neg(0,x) = neg(x) .
  eq  test_abs_neg(0z(x),y) = test_abs_neg(x,y) .
  eq  test_abs_neg(1z(x),y) = test_abs_pos(x,y) .
  eq  test_abs_neg(jz(x),y) = test_abs_neg(x,y) .

  eq  signe(0) = 0 .
  eq  signe(0z(x)) = signe(x) .
  eq  signe(1z(x)) = test_signe_pos(x) .
  eq  signe(jz(x)) = test_signe_neg(x) .

  eq  test_signe_pos(0) = 1z(0) .
  eq  test_signe_pos(0z(x)) = test_signe_pos(x) .
  eq  test_signe_pos(1z(x)) = test_signe_pos(x) .
  eq  test_signe_pos(jz(x)) = test_signe_neg(x) .

  eq  test_signe_neg(0) = jz(0) .
  eq  test_signe_neg(0z(x)) = test_signe_neg(x) .
  eq  test_signe_neg(1z(x)) = test_signe_pos(x) .
  eq  test_signe_neg(jz(x)) = test_signe_neg(x) .

  eq  min(x,y) = test_min_pos(-(abs(y), abs(x)), x, y) .
  eq  min'(x,y) = test_min_pos(-(abs(1z(y)), abs(1z(x))), x, y) .
  eq  min''(x,y) = test_min_pos(-(abs(jz(y)), abs(jz(x))), x, y) .

  eq  test_min_pos(0, x, y) = x .
  eq  test_min_pos(0z(x), y, z) = test_min_pos(x, y, z) .
  eq  test_min_pos(1z(x), y, z) = test_min_pos(x, y, z) .
  eq  test_min_pos(jz(x), y, z) = test_min_neg(x, y, z) .

  eq  test_min_neg(0, x, y) = y .
  eq  test_min_neg(0z(x), y, z) = test_min_neg(x, y, z) .
  eq  test_min_neg(1z(x), y, z) = test_min_pos(x, y, z) .
  eq  test_min_neg(jz(x), y, z) = test_min_neg(x, y, z) .

  eq  f(0,x) = 0 .
  eq  f(x,0) = signe(x) .

  eq  f(0z(x),0z(y)) = f(x,y) .
  eq  f(0z(x),1z(y)) = 0z(f(x,1z(y))) .
  eq  f(0z(x),jz(y)) = 0z(f(x,jz(y))) .

  eq  f(1z(x),0z(y)) = f(1z(x),y) .

  eq  f(1z(x),1z(y)) = +(0z(f(-(x, min'(x,y)), 1z(y))), 
                         f(min(1z(x),1z(y)), -(x, y))) .

  eq  f(1z(x),jz(y)) = +(0z(f(+(x, min''(neg(x),y)), jz(y))), 
                         f(min(1z(x),1z(neg(y))), +(x, y))) .

  eq  f(jz(x),0z(y)) = f(jz(x),y) .

  eq  f(jz(x),1z(y)) = +(0z(f(+(x, min'(neg(x),y)), 1z(y))), 
                         f(min(jz(x),jz(neg(y))), +(x, y))) .

  eq f(jz(x),jz(y)) = +(0z(f(-(x, min''(x,y)), jz(y))),
                        f(min(jz(x),jz(y)), -(x, y))) .

  eq  rat(x,y) = irred(*(signe(y),f(x,y)),*(signe(y),f(y,x))) .
 
  eq plus(irred(x,y), irred(u,v)) = rat(+(*(x, v), *(u, y)), *(y, v)) .

  eq times(irred(x,y), irred(u,v)) = rat(*(x,u),*(y,v)) .

endo 

  red rat(0,jz(1z(0))) .

  red times(irred(1z(0),jz(1z(0))), irred(1z(0),1z(0)))  .

  red *(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(0)))))))))))))))))))))))))))))))))))))))))), 1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(1z(0))))))))))))))))))))))))))))))))))))))))) .

