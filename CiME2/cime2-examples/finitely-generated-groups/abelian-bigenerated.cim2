(* Completion of abelian group bigenerated. *)
(* See Epstein: Word processing in groups. p126 (EPST002) *)

(* The transpositions *)

let F = word_signature "x y X Y"; (* capital letter is the inverse *)

(* An order *)
let o1 = length_lex (word_precedence F "x<X<y<Y");
let o2 = length_lex (word_precedence F "x<y<X<Y");

(* The string rewriting system *)

let R = SRS F "
  X x -> ;
  x X -> ;
 
  Y y -> ;
  y Y -> ;

  y x -> x y ;

(*  Y x -> x Y ;
  y X -> X y ;
  Y X -> X Y ;*)
";

(* The completion *)
#verbose;

let C1 = word_completion o1 R ;
(* result : 
" X x -> ;
  x X -> ;
 
  Y y -> ;
  y Y -> ;

  y x -> x y ;

  Y x -> x Y ;
  y X -> X y ;
  Y X -> X Y ;";
*)


let C2 = word_completion o2 R ;
(* result : non termination of completion but :
" X x -> ;
  x X -> ;
 
  Y y -> ;
  y Y -> ;

  y x -> x y ;

  Y x -> x Y ;
  X y -> y X ;
  Y X -> X Y ;
  
  x y^n X -> y ^n | n>0;
  y X^n Y -> X^n | n>0;
";
*)


