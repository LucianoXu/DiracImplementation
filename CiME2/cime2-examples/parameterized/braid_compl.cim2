(* Artin's Braid group *)

let N = parameters "n";

let S = pword_signature N 
	"t{i} | {1<= i <= n-1 };
	 T{i} | {1<= i <= n-1 };" ;

(* Does not terminate because of rule 3 *)
let R = psrs S 
	"
	T{i} t{i} -> | { 1<= i <= n-1};
	t{i} T{i} -> | { 1<= i <= n-1};	
	t{i} t{j} -> t{j} t{i} | { 1 <= i <= n-1 /\ 1<= j <= n-1 
				  /\ (i-j > 1 \/ i-j < -1)};
	t{i} t{i+1} t{i} -> t{i+1} t{i} t{i+1} | { 1 <= i <= n-2};	
	";



(* This is the same one but we orient the third rule *)
let Rbis = psrs S 
	"
	T{i} t{i} -> | { 1<= i <= n-1};
	t{i} T{i} -> | { 1<= i <= n-1};	
	
	t{i} t{j} -> t{j} t{i} | { 1 <= i <= n-1 /\ 1<= j <= n-1 
				  /\ i-j > 1};
	
	t{i} t{i+1} t{i} -> t{i+1} t{i} t{i+1} | { 1 <= i <= n-2};	
	";

 pconfluent Rbis ;


let Rcomplete = psrs S 
	"
	T{i} t{i} -> | { 1<= i <= n-1};
	t{i} T{i} -> | { 1<= i <= n-1};	
	t{i} t{j} -> t{j} t{i} | { 1 <= i <= n-1 /\ 1<= j <= n-1 
				  /\ i-j > 1};

	t{i} t{i+1} t{i} -> t{i+1} t{i} t{i+1} | { 1 <= i <= n-2};	

	T{i} t{j} -> t{j} t{i} T{j} T{i} | {1 <= i <= n-1 /\ 1<= j <= n-1 
				  		/\ (i-j = 1 \/ i-j = -1)};
	
	T{i} t{j} -> t{j} T{i} | {1 <= i <= n-1 /\ 1<= j <= n-1 
				  /\ (i-j > 1 \/ i-j < -1)};
	";


let Rcompletebis = psrs S 
	"
	T{i} t{i} -> | { 1<= i <= n-1};
	t{i} T{i} -> | { 1<= i <= n-1};	
	t{i} t{j} -> t{j} t{i} | { 1 <= i <= n-1 /\ 1<= j <= n-1 
				  /\ (i-j > 1)};

	t{i} t{i+1} t{i} -> t{i+1} t{i} t{i+1} | { 1 <= i <= n-2};	

	T{i} t{j} -> t{j} t{i} T{j} T{i} | {1 <= i <= n-1 /\ 1<= j <= n-1 
				  		/\ i-j = 1 };
	
	T{i} t{j} -> t{j} t{i} T{j} T{i} | {1 <= i <= n-1 /\ 1<= j <= n-1 
				  		/\ i-j = -1};

	T{i} t{j} -> t{j} T{i} | {1 <= i <= n-1 /\ 1<= j <= n-1 
				  /\ i-j > 1 };
	T{i} t{j} -> t{j} T{i} | {1 <= i <= n-1 /\ 1<= j <= n-1 
				  /\ i-j < -1};
	";



let Rcompletebismodified = psrs S 
	"
	T{i} t{i} -> | { 1<= i <= n-1};
	t{i} T{i} -> | { 1<= i <= n-1};	

	t{i} t{j} -> t{j} t{i} | { 1 <= i <= n-1 /\ 1<= j <= n-1 
				  /\ (i-j > 1)}; 


	t{i+1} t{i} t{i+1} -> t{i} t{i+1} t{i}  | { 1 <= i <= n-2};	


	T{j+1} t{j} -> t{j} t{j+1} T{j} T{j+1} | 
	     { 1<= j <= n-2 };
	T{j-1} t{j} -> t{j} t{j-1} T{j} T{j-1} | 
		{ 2<= j <= n-1 };

	T{i} t{j} -> t{j} T{i} | {1 <= i <= n-1 /\ 1<= j <= n-1 
				  /\ i-j > 1 };
	T{i} t{j} -> t{j} T{i} | {1 <= i <= n-1 /\ 1<= j <= n-1 
				  /\ i-j < -1};

	t{i} t{j} t{j + 1} T{j} T{j + 1} -> t{j}  | 
		{i = j + 1 and j <= n + - 2 and 1 <= j and 1 <= i and
                     i <= n + - 1};

	t{i} t{j} t{j + - 1} T{j} T{j + - 1} -> t{j}
		| {i = j + - 1 and 
		   j <= n + - 1 and 
		   2 <= j and
                   1 <= i and 
	           i <= n + - 1};
";

pconfluent Rcompletebismodified;

#mem;
#time;
#quit;
