(*

  Evelyne Contejean, Claude Marché, Landy Rabehasaina, RTA'97

  AC rewrite system for addition and multiplication of integers, in
  ternary representation

*)

[y, x]

AC [*, +]

0(#) -> # ;
+(#,x) -> x ;
+(0(x),0(y)) -> 0(+(x,y)) ;
+(0(x),1(y)) -> 1(+(x,y)) ;
+(0(x),j(y)) -> j(+(x,y)) ;
+(1(x),1(y)) -> j(+(1(#),+(x,y))) ;
+(j(x),j(y)) -> 1(+(j(#),+(x,y))) ;
+(1(x),j(y)) -> 0(+(x,y)) ;
opp(#) -> # ;
opp(0(x)) -> 0(opp(x)) ;
opp(1(x)) -> j(opp(x)) ;
opp(j(x)) -> 1(opp(x)) ;
-(x,y) -> +(opp(y),x) ;
*(#,x) -> # ;
*(0(x),y) -> 0(*(x,y)) ;
*(1(x),y) -> +(0(*(x,y)),y) ;
*(j(x),y) -> -(0(*(x,y)),y) ;

(*
let F = signature "
     # : constant;
     0,1,j : postfix unary;
     opp : unary;
     - : infix binary;
     +,* : AC;
";

let X = vars "x y";

let R = HTRS {} F X "
     (#)0 -> #;
     # + x -> x;
     (x)0 + (y)0 -> (x+y)0;
     (x)0 + (y)1 -> (x+y)1;
     (x)0 + (y)j -> (x+y)j;
     (x)1 + (y)1 -> (x+y+(#)1)j;
     (x)j + (y)j -> (x+y+(#)j)1;
     (x)1 + (y)j -> (x+y)0;
     opp(#) -> # ;
     opp((x)0) -> (opp(x))0 ;
     opp((x)1) -> (opp(x))j ;
     opp((x)j) -> (opp(x))1 ;
     x - y -> x+opp(y) ;
     # * x -> #;
     (x)0 * y -> (x*y)0;
     (x)1 * y -> (x*y)0 + y;
     (x)j * y -> (x*y)0 - y;
";


termcrit "minimal";
termcrit "nomarks";
polyinterpkind { ("linear",1) };
h_termination R;
output_last_proof "";
export_htrs R;

*)
